buildscript {
    ext {
        buildToolsVersion = "36.1.0"
        minSdkVersion = 28
        compileSdkVersion = 36
        targetSdkVersion = 36
        ndkVersion = "29.0.14206865"
        cmakeVersion = "3.31.6"
        javaVersion = JavaVersion.VERSION_17
        kotlinVersion = "2.3.10"
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "com.facebook.react.rootproject" apply false
    id "expo-root-project" apply false
    id "com.google.gms.google-services" version "4.4.4" apply false
    id "com.google.firebase.crashlytics" version "3.0.6" apply false
}

subprojects { subproject ->
    afterEvaluate {
        if (subproject.hasProperty("android")) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
                compileSdkVersion rootProject.ext.compileSdkVersion
                ndkVersion rootProject.ext.ndkVersion
                externalNativeBuild {
                    cmake {
                        version rootProject.ext.cmakeVersion
                    }
                }
                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }
                compileOptions {
                    sourceCompatibility = rootProject.ext.javaVersion
                    targetCompatibility = rootProject.ext.javaVersion
                }
                if (subproject.plugins.hasPlugin("org.jetbrains.kotlin.android")) {
                    kotlinOptions {
                        jvmTarget = rootProject.ext.javaVersion.majorVersion
                    }
                }
            }
        }
    }
}
